<html>
<head>
<style>
table {
  table-layout:fixed;
  font-family: arial, sans-serif;
  border-collapse: collapse;
  font-size: 14px;
}

td, th {
  border: 1px solid #111111;
  text-align: left;
  padding: 8px;
}

td.instruction {
   font-weight: bold;
}

tr:nth-child(even) {
  background-color: #dddddd;
}
.code {
   font-size: 12px;
   box-sizing: border-box;
   border: 2px solid #ccc;
   border-radius: 4px;
   background-color: #f8f8f8; 
   font-family: "Lucida Console", "Courier New", monospace;
   line-height: 150%;
}
</style>

</head>
<body>
<h4>Multiply two 16 bit numbers:</h4>
<textarea class="code" id="multiply" rows="10" cols="80" readonly>
Start:   LD R0 Op1
         LD R2 Op2
         ; R0 * R2 -> R1 R2; temp R3
         LDLZ R1 0 ;
         LDLZ R3 16
         SHR R2 R2
BitLoop: BCC NoAdd
         ADD R1 R1 R0
NoAdd:   SHR R1 R1
         ROR R2 R2
         DEC R3 R3
         BZC BitLoop
         HLT
Op1:     .data 500
Op2:     .data 13
</textarea>
<p><button onclick="copyCode('multiply')">Copy code</button></p>
<h4>Divide two 16 bit numbers:</h4>
<textarea class="code" id="divide" rows="10" cols="80" readonly>
Start:    LD R1 Divident
          LD R2 Divisor
          ; Divide R1 by R2. Result in R1. Remainder in R0. Uses R3.
          LDLZ R0 0
          LDLZ R3 16
          SHL R1 R1
BitLoop:  ROL R0 R0
          CMP R2 R0
          BCC NoSub
          SUB R0 R0 R2
          SEC 
NoSub:    ROL R1 R1
          DEC R3 R3
          BZC BitLoop
          HLT
Divident: .data 100
Divisor:  .data 13
</textarea>
<p><button onclick="copyCode('divide')">Copy code</button></p>
<h4>Display the value of three registers:</h4>
<textarea class="code" id="displayreg" rows="10" cols="80" readonly>
.define IoCntrLo  0xFFFD
.define IoCntrHi  0xFFFE
.define FrameBuf  0x8000 
               LDHZ R4 0x00
Start:         LDHZ SP 0x01 
               LDH R3 IoCntrLo.H
               LDL R3 IoCntrLo.L
               LD  R2 [R3]
               LDLZ R0 1
               LDLZ R1 4
               JSR DisplayReg
               LDL R3 IoCntrHi.L
               LD R2 [R3]
               LDLZ R0 1
               LDLZ R1 5
               JSR DisplayReg
               LD R2 R4
               LDLZ R0 1
               LDLZ R1 7
               JSR DisplayReg
               INC R4 R4
               JMP Start

; [in]   R0 = column
; [in]   R1 = row
; [in]   R2 = reg
DisplayReg:    PSH R3
               PSH R4
               PSH R5
               JSR GetCursorPos
               LDLZ R4 0x0F
               LDH R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               JSR WriteHexDigit
               LDH R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               LDL R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               JSR WriteHexDigit
               LDL R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               POP R5
               POP R4
               POP R3

; [in]   R0 = column
; [in]   R1 = row
; [out]  R5 = position 
GetCursorPos:  PSH R3
               LDHZ R5 FrameBuf.H
               LD R3  R1 ; Multiply row by 40 
               SHL R3 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               ADD R5 R5 R0 ; Add column
               POP R3
               RET

; [in]   R5 = pos
; [in]   R3 = hex digit
WriteHexDigit: PSH R4
               LDLZ R4 10
               CMP R3 R4
               BCC NotDecimal
               LDLZ R4 48 ; '0'
               ADD R3 R3 R4
               JMP StoreDigit
NotDecimal:    LDLZ R4 55 ; 'A' - 10
               ADD R3 R3 R4
StoreDigit:    LDH R3 0x3C; White background red text
               STO R3 [R5] 
               INC R5 R5
               POP R4
               RET
</textarea>
<p><button onclick="copyCode('displayreg')">Copy code</button></p>

</body>
</html>



