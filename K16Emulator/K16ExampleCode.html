<html>
<head>
<link rel="stylesheet" href="K16.css">
</head>
<body>
<h4><button onclick="copyCode('multiply')">Copy code</button> Multiply two 16 bit numbers:</h4>
<textarea class="code" id="multiply" rows="20" style="width: 910px" readonly>
          LDHZ SP 0x01 ; Set SP to 0x0100
Start:    LD R0 Op1
          LD R2 Op2
          JSR Multiply
          HLT
Op1:      .data 500
Op2:      .data 13

; Multiply  R0 * R2 -> R1 R2
; [in]     R0 = operand1
; [out]    R1 = result high
; [in/out] R2 = operand2/result low
Multiply: PSH R3
          LDLZ R1 0 
          LDLZ R3 16
          SHR R2 R2
BitLoop:  BCC NoAdd
          ADD R1 R1 R0
NoAdd:    SHR R1 R1
          ROR R2 R2
          DEC R3 R3
          BZC BitLoop
          POP R3
          RET
</textarea>
<h4><button onclick="copyCode('divide')">Copy code</button> Divide two 16 bit numbers:</h4>
<textarea class="code" id="divide" rows="20" style="width: 910px" readonly>
          LDHZ SP 0x01 ; Set SP to 0x0100
Start:    LD R1 Divident
          LD R2 Divisor
          JSR Divide
          HLT
Divident: .data 100
Divisor:  .data 13

; Divide R1 by R2 -> result in R1, remainder in R0
; [out]    R0 = remainder
; [in/out] R1 = divident/result
; [in]     R2 = divisor
Divide:   PSH R3
          LDLZ R0 0
          LDLZ R3 16
          SHL R1 R1
BitLoop:  ROL R0 R0
          CMP R2 R0
          BCC NoSub
          SUB R0 R0 R2
          SEC 
NoSub:    ROL R1 R1
          DEC R3 R3
          BZC BitLoop
          POP R3
          RET
</textarea>
<h4><button onclick="copyCode('displayreg')">Copy code</button> Display the value of three registers:</h4>
<textarea class="code" id="displayreg" rows="20" style="width: 910px" readonly>
.define $AddrSwtch 0xFFF8
.define $CtrlSwtch 0xFFF9
.define $AddrLeds  0xFFFA
.define $DataLeds  0xFFFB
.define $RegSwtch  0xFFFC
.define $IoCntrLo  0xFFFD
.define $IoCntrHi  0xFFFE

.define $FrameBuf  0x8000 

.define $Black     0x0000
.define $Blue      0x0001
.define $Green     0x0002
.define $Cyan      0x0003
.define $Red       0x0004
.define $Magenta   0x0005
.define $Yellow    0x0006
.define $White     0x0007

.define $BgBlack   ($Black << 3)
.define $BgBlue    ($Blue << 3)
.define $BgGreen   ($Green << 3)
.define $BgCyan    ($Cyan << 3)
.define $BgRed     ($Red << 3)
.define $BgMagenta ($Magenta << 3)
.define $BgYellow  ($Yellow << 3)
.define $BgWhite   ($White << 3)

               LDHZ SP 0x01 ; Set SP to 0x0100
Start:         LDHZ R4 0x00 
               LDH R3 $IoCntrLo.H
               LDL R3 $IoCntrLo.L
               LD  R2 [R3]
               LDLZ R0 1
               LDLZ R1 4
               JSR DisplayReg
               LDL R3 $IoCntrHi.L
               LD R2 [R3]
               LDLZ R0 1
               LDLZ R1 5
               JSR DisplayReg
               LD R2 R4
               LDLZ R0 1
               LDLZ R1 7
               JSR DisplayReg
               INC R4 R4
               JMP Start

; Display the value of R2 in hexadecimal format at screen column R0, and row R1
; [in]   R0 = column
; [in]   R1 = row
; [in]   R2 = reg
DisplayReg:    PSH R3
               PSH R4
               PSH R5
               JSR GetCursorPos
               LDLZ R4 0x0F
               SWP R2 R2
               LDL R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               AND R3 R3 R4
              JSR WriteHexDigit
               LDL R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               SWP R2 R2
               LDL R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               AND R3 R3 R4
              JSR WriteHexDigit
               LDL R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               POP R5
               POP R4
               POP R3
               RET

; Get the address of screen column R0, row R1 
; [in]   R0 = column
; [in]   R1 = row
; [out]  R5 = position 
GetCursorPos:  PSH R3
               LDHZ R5 $FrameBuf.H
               LD R3  R1 ; Multiply row by 40 
               SHL R3 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               ADD R5 R5 R0 ; Add column
               POP R3
               RET

; Write hex digit R3 at position R5 to screen
; [in]   R5 = pos
; [in]   R3 = hex digit
WriteHexDigit: PSH R4
               LDLZ R4 10
               CMP R3 R4
               BCC NotDecimal
               LDLZ R4 '0'
               ADD R3 R3 R4
               JMP StoreDigit
NotDecimal:    LDLZ R4 'A' - 10
               ADD R3 R3 R4
StoreDigit:    LDH R3 $BgWhite | $Red; White background red text
               STO R3 [R5] 
               INC R5 R5
               POP R4
               RET
</textarea>
<h4><button onclick="copyCode('drawPixels')">Copy code</button> Draw some pixels:</h4>
<textarea class="code" id="drawPixels" rows="20" style="width: 910px" readonly>

.define $AddrSwtch 0xFFF8
.define $CtrlSwtch 0xFFF9
.define $AddrLeds  0xFFFA
.define $DataLeds  0xFFFB
.define $RegSwtch  0xFFFC
.define $IoCntrLo  0xFFFD
.define $IoCntrHi  0xFFFE
 
.define $FrameBuf  0x8000 
 
.define $Black     0x0000
.define $Blue      0x0001
.define $Green     0x0002
.define $Cyan      0x0003
.define $Red       0x0004
.define $Magenta   0x0005
.define $Yellow    0x0006
.define $White     0x0007
 
.define $BgBlack   ($Black << 3)
.define $BgBlue    ($Blue << 3)
.define $BgGreen   ($Green << 3)
.define $BgCyan    ($Cyan << 3)
.define $BgRed     ($Red << 3)
.define $BgMagenta ($Magenta << 3)
.define $BgYellow  ($Yellow << 3)
.define $BgWhite   ($White << 3)
 
.define $Mask_x0y0 ~(0x0007)
.define $Mask_x1y0 ~(0x0007 << 3)
.define $Mask_x0y1 ~(0x0007 << 6)
.define $Mask_x1y1 ~(0x0007 << 9)
 
          
               LDHZ SP 0x01 ; Set SP to 0x0100

               LDLZ R0 0x00
               LDLZ R1 0x00
               LDLZ R2 $Green.L
               JSR DrawPixel;
               INC R0 R0
               JSR DrawPixel
               INC R0 R0
               JSR DrawPixel
               INC R1 R1
               JSR DrawPixel
               INC R1 R1
               JSR DrawPixel
               HLT
; Draw pixel
; [in] R0 = x
; [in] R1 = y
; [in] R2 = color   
DrawPixel:     PSH R0
               PSH R1
               PSH R2
               LDLZ R3 0x01
               AND R4 R1 R3
               BZC Pixel_y1
Pixel_y0:      AND R4 R0 R3
               BZC Pixel_y0x1
Pixel_y0x0:    LDL R4 $Mask_x0y0.L                   
               LDH R4 $Mask_x0y0.H
               JMP StorePixel
Pixel_y0x1:    SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               LDL R4 $Mask_x1y0.L                   
               LDH R4 $Mask_x1y0.H 
               JMP StorePixel        
Pixel_y1:      SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               AND R4 R0 R2
               BZC Pixel_y1x1
Pixel_y1x0:    LDL R4 $Mask_x0y1.L                   
               LDH R4 $Mask_x0y1.H
               JMP StorePixel
Pixel_y1x1:    SHL R2 R2
               SHL R2 R2
               SHL R2 R2
               LDL R4 $Mask_x1y1.L                   
               LDH R4 $Mask_x1y1.H
StorePixel:    SHR R0 R0
               SHR R1 R1
               JSR GetCursorPos
               LD R3 [R5]
               AND R3 R3 R4    
               OR R3 R3 R2
               LDHZ R2 0x80
               OR R3 R3 R2
               STO R3 [R5]        
               POP R2
               POP R1
               POP R0
               RET            
              
; Calculate cursor position              
; [in]   R0 = column
; [in]   R1 = row
; [out]  R5 = position 
GetCursorPos:  PSH R3
               LDHZ R5 $FrameBuf.H
               LD R3  R1 ; Multiply row by 40 
               SHL R3 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               ADD R5 R5 R0 ; Add column
               POP R3
               RET
</textarea>
<h4><button onclick="copyCode('clearScreen')">Copy code</button> Clear Screen:</h4>
<textarea class="code" id="clearScreen" rows="20" style="width: 910px" readonly>

.define $FrameBuf  0x8000 
 
.define $Black     0x0000
.define $Blue      0x0001
.define $Green     0x0002
.define $Cyan      0x0003
.define $Red       0x0004
.define $Magenta   0x0005
.define $Yellow    0x0006
.define $White     0x0007
 
.define $BgBlack   ($Black << 3)
.define $BgBlue    ($Blue << 3)
.define $BgGreen   ($Green << 3)
.define $BgCyan    ($Cyan << 3)
.define $BgRed     ($Red << 3)
.define $BgMagenta ($Magenta << 3)
.define $BgYellow  ($Yellow << 3)
.define $BgWhite   ($White << 3)
         
               LDHZ SP 0x01 ; Set SP to 0x0100
               LDLZ R0 ' '
               LDLZ R1 $BgCyan | $Magenta
               JSR FillScreen
               HLT
               
; Fill screeen
; [in] R0 = character
; [in] R1 = color           
FillScreen:    PSH R2
               PSH R3
               PSH R4
               LDL R2 R0 ; combine character and color
               LDH R2 R1
               LDHZ R3 $FrameBuf.H ; Start address
               LDL R4 (30 * 40).L  ; end address + 1
               LDH R4 (30 * 40).H
               ADD R4 R4 R3
CharacterLoop: STO R2 [R3]
               INC R3 R3
               CMP R3 R4
               BZC CharacterLoop               
               POP R4
               POP R3
               POP R2
               RET
</textarea>
<h4><button onclick="copyCode('calculator')">Copy code</button> Calculator:</h4>
<textarea class="code" id="calculator" rows="20" style="width: 910px" readonly>
.define $FrameBuf  0x8000 
.define $AddrSwtch 0xFFF8
.define $CtrlSwtch 0xFFF9
.define $AddrLeds  0xFFFA
.define $DataLeds  0xFFFB
.define $RegSwtch  0xFFFC

.define $Black     0x0000
.define $Blue      0x0001
.define $Green     0x0002
.define $Cyan      0x0003
.define $Red       0x0004
.define $Magenta   0x0005
.define $Yellow    0x0006
.define $White     0x0007
 
.define $BgBlack   ($Black << 3)
.define $BgBlue    ($Blue << 3)
.define $BgGreen   ($Green << 3)
.define $BgCyan    ($Cyan << 3)
.define $BgRed     ($Red << 3)
.define $BgMagenta ($Magenta << 3)
.define $BgYellow  ($Yellow << 3)
.define $BgWhite   ($White << 3)

               JMP Start

Op1String: .string "Op1: "
Op2String: .string "Op2: "
ResString: .string "Res: "
Op1:       .data 0
Op2:       .data 0
         
Start:         LDHZ SP 0x01 ; Set SP to 0x0100
               LDLZ R0 ' '
               LDLZ R1 $BgCyan | $Magenta
               JSR FillScreen
               LDLZ R0 0x01; column 1
               LDLZ R1 0x01; row 1
               LDLZ R2 Op1String.L
               JSR DisplayString
               HLT
               LDL R3 $AddrSwtch.L
               LDH R3 $AddrSwtch.H
               LD R2 [R3]
               STO R2 Op1
               LDLZ R0 0x06; column 1
               LDLZ R1 0x01; row 1               
               JSR DisplayReg
               LDLZ R0 0x01; column 1
               LDLZ R1 0x02; row 1
               LDLZ R2 Op2String.L
               JSR DisplayString
               HLT
               LDL R3 $AddrSwtch.L
               LDH R3 $AddrSwtch.H
               LD R2 [R3]
               STO R2 Op2
               LDLZ R0 0x06; column 1
               LDLZ R1 0x02; row 1               
               JSR DisplayReg
               LDLZ R0 0x01; column 1
               LDLZ R1 0x03; row 1
               LDLZ R2 ResString.L
               JSR DisplayString
               LD R2 Op1
               LD R3 Op2
               ADD R2 R2 R3
               LDLZ R0 0x06; column 1
               LDLZ R1 0x03; row 1               
               JSR DisplayReg
               HLT
               JMP Start
; Fill screeen
; [in] R0 = character
; [in] R1 = color           
FillScreen:    PSH R2
               PSH R3
               PSH R4
               LDL R2 R0 ; combine character and color
               LDH R2 R1
               LDHZ R3 $FrameBuf.H ; Start address
               LDL R4 (30 * 40).L  ; end address + 1
               LDH R4 (30 * 40).H
               ADD R4 R4 R3
CharacterLoop: STO R2 [R3]
               INC R3 R3
               CMP R3 R4
               BZC CharacterLoop               
               POP R4
               POP R3
               POP R2
               RET
               
; Display the value of R2 in hexadecimal format at screen column R0, and row R1
; [in]   R0 = column
; [in]   R1 = row
; [in]   R2 = reg
DisplayReg:    PSH R3
               PSH R4
               PSH R5
               JSR GetCursorPos
               LDLZ R4 0x0F
               SWP R2 R2
               LDL R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               AND R3 R3 R4
               JSR WriteHexDigit
               LDL R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               SWP R2 R2
               LDL R3 R2
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               SHR R3 R3
               AND R3 R3 R4
               JSR WriteHexDigit
               LDL R3 R2
               AND R3 R3 R4
               JSR WriteHexDigit
               POP R5
               POP R4
               POP R3
               RET

; Write string at position R5 to screen
; [in]   R0 = column
; [in]   R1 = row
; [in]   R2 = address of string
DisplayString:     PSH R1
                   PSH R2
                   PSH R3
                   PSH R4
                   PSH R5
                   JSR GetCursorPos
                   LDH R4 0x00
                   LDH R1 $BgWhite | $Red; White background red text
DisplayStringLoop: LD  R3 [R2]
                   SWP R3 R3
                   LDL R4 R3 
                   BZS DisplayStringDone
                   LDL R1 R4
                   STO R1 [R5]
                   INC R5 R5
                   SWP R3 R3
                   LDL R4 R3 
                   BZS DisplayStringDone
                   LDL R1 R4
                   STO R1 [R5]
                   INC R5 R5
                   INC R2 R2;
                   JMP DisplayStringLoop
DisplayStringDone: POP R5
                   POP R4
                   POP R3
                   POP R2
                   POP R1
                   RET

; Get the address of screen column R0, row R1 
; [in]   R0 = column
; [in]   R1 = row
; [out]  R5 = position 
GetCursorPos:  PSH R3
               LDHZ R5 $FrameBuf.H
               LD R3  R1 ; Multiply row by 40 
               SHL R3 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               ADD R5 R5 R0 ; Add column
               POP R3
               RET

; Write hex digit R3 at position R5 to screen
; [in]   R5 = pos
; [in]   R3 = hex digit
WriteHexDigit: PSH R4
               LDLZ R4 10
               CMP R3 R4
               BCC NotDecimal
               LDLZ R4 '0'
               ADD R3 R3 R4
               JMP StoreDigit
NotDecimal:    LDLZ R4 'A' - 10
               ADD R3 R3 R4
StoreDigit:    LDH R3 $BgWhite | $Red; White background red text
               STO R3 [R5] 
               INC R5 R5
               POP R4
               RET
</textarea>
<h4><button onclick="copyCode('drawHorizVert')">Copy code</button> Draw horizontal and vertical lines:</h4>
<textarea class="code" id="drawHorizVert" rows="20" style="width: 910px" readonly>
.define $AddrSwtch 0xFFF8
.define $CtrlSwtch 0xFFF9
.define $AddrLeds  0xFFFA
.define $DataLeds  0xFFFB
.define $RegSwtch  0xFFFC
.define $IoCntrLo  0xFFFD
.define $IoCntrHi  0xFFFE
 
.define $FrameBuf  0x8000 
 
.define $Black     0x0000
.define $Blue      0x0001
.define $Green     0x0002
.define $Cyan      0x0003
.define $Red       0x0004
.define $Magenta   0x0005
.define $Yellow    0x0006
.define $White     0x0007
 
.define $BgBlack   ($Black << 3)
.define $BgBlue    ($Blue << 3)
.define $BgGreen   ($Green << 3)
.define $BgCyan    ($Cyan << 3)
.define $BgRed     ($Red << 3)
.define $BgMagenta ($Magenta << 3)
.define $BgYellow  ($Yellow << 3)
.define $BgWhite   ($White << 3)
           
               LDHZ SP 0x01 ; Set SP to 0x0100
               LDLZ R0 $Black
               JSR FillPixelScreen
               
               LDLZ R4 8
Start:         LDLZ R0 0x00
               LDLZ R1 0x00
               LDLZ R2 0
               LDLZ R3 60
DrawLoop1:     JSR DrawVline;
               INC R0 R0
               INC R2 R2
               CMP R2 R4
               BZC DrawLoop1
               LDLZ R0 0
               LDLZ R2 0
               LDLZ R3 80
DrawLoop2:     JSR DrawHline
               INC R1 R1
               INC R2 R2
               CMP R2 R4
               BZC DrawLoop2
               JMP Start

; Draw horizontal line
; [in] R0 = x
; [in] R1 = y
; [in] R2 = color   
; [in] R3 = length
DrawHline:     PSH R0
               PSH R3
DrawHlineLoop: JSR DrawPixel
               INC R0 R0
               DEC R3 R3
               BZC DrawHlineLoop
               POP R3
               POP R0
               RET
; Draw vertical line
; [in] R0 = x
; [in] R1 = y
; [in] R2 = color   
; [in] R3 = length
DrawVline:     PSH R1
               PSH R3
DrawVlineLoop: JSR DrawPixel
               INC R1 R1
               DEC R3 R3
               BZC DrawVlineLoop
               POP R3
               POP R1
               RET
; Draw pixel
; [in] R0 = x
; [in] R1 = y
; [in] R2 = color   
DrawPixel:     PSH R0
               PSH R1
               PSH R2
               PSH R3
               PSH R4
               PSH R5
               
               ; Calculate color map address
               LDL R3 ColorMap.L
               LDH R3 ColorMap.H
               ADD R2 R2 R3
               LD R2 [R2]
               
               LDLZ R3 0
               SHR R1 R1 ; 
               ROL R3 R3
               SHR R0 R0
               ROL R3 R3
               
               LDL R4 PixelMask.L
               LDH R4 PixelMask.H
               ADD R3 R3 R4
               LD R3 [R3]
               AND R2 R2 R3
               NOT R3 R3
               JSR GetCursorPos
               LD R4 [R5]
               AND R4 R4 R3
               OR R4 R4 R2
               STO R4 [R5]
               
               POP R5
               POP R4
               POP R3      
               POP R2
               POP R1
               POP R0
               RET            
ColorMap: 
.data $Black   | ($Black << 3)   | ($Black << 6)   | ($Black << 9)   | 0x8000
.data $Blue    | ($Blue << 3)    | ($Blue << 6)    | ($Blue << 9)    | 0x8000
.data $Green   | ($Green << 3)   | ($Green << 6)   | ($Green << 9)   | 0x8000
.data $Cyan    | ($Cyan << 3)    | ($Cyan << 6)    | ($Cyan << 9)    | 0x8000
.data $Red     | ($Red << 3)     | ($Red << 6)     | ($Red << 9)     | 0x8000
.data $Magenta | ($Magenta << 3) | ($Magenta << 6) | ($Magenta << 9) | 0x8000
.data $Yellow  | ($Yellow << 3)  | ($Yellow << 6)  | ($Yellow << 9)  | 0x8000
.data $White   | ($White << 3)   | ($White << 6)   | ($White << 9)   | 0x8000
     
PixelMask:
.data 0x0007      | 0x8000
.data 0x0007 << 3 | 0x8000
.data 0x0007 << 6 | 0x8000
.data 0x0007 << 9 | 0x8000
              
; Calculate cursor position              
; [in]   R0 = column
; [in]   R1 = row
; [out]  R5 = position 
GetCursorPos:  PSH R3
               LDHZ R5 $FrameBuf.H
               LD R3  R1 ; Multiply row by 40 
               SHL R3 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               SHL R3 R3
               SHL R3 R3
               ADD R5 R5 R3
               ADD R5 R5 R0 ; Add column
               POP R3
               RET
; Fill pixel screen with a color
; [in] R0 = color
FillPixelScreen: PSH R2
                 PSH R3
                 PSH R4
                 
                 ; Calculate color map address
                 LDL R2 ColorMap.L
                 LDH R2 ColorMap.H
                 ADD R2 R2 R0
                 
                 ; Load R2 with pixel block color
                 LD R2 [R2]
                 
                 LDHZ R3 $FrameBuf.H ; Start address
                 LDL R4 (30 * 40).L  ; end address + 1
                 LDH R4 (30 * 40).H
                 ADD R4 R4 R3
PixelBlockLoop:  STO R2 [R3]
                 INC R3 R3
                 CMP R3 R4
                 BZC PixelBlockLoop
                 POP R4
                 POP R3
                 POP R2
                 RET

</textarea>
<h4><button onclick="copyCode('testCpu')">Copy code</button> Test CPU:</h4>
<textarea class="code" id="testCpu" rows="20" style="width: 910px" readonly>
               LDHZ SP 0x02 ; Set SP to 0x0200
               JMP Start
                          
TestOk:        .data 0

Start:         LDL R5 Error.L
               LDH R5 Error.H
               
               LDL R1 Operand1_1.L     ; Test ADD
               LDH R1 Operand1_1.H
               LD R1 [R1]
               LDL R2 Operand2_1.L
               LDH R2 Operand2_1.H
               LD R2 [R2]
               SEC
               ADD R3 R1 R2
               LDL R4 Result_Add_1.L
               LDH R4 Result_Add_1.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
              
               CLC                     ; Test ADC - Carry 0
               ADC R3 R1 R2
               LDL R4 Result_Add_1.L
               LDH R4 Result_Add_1.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
               
               SEC                     ; Test ADC - Carry 1
               ADC R3 R1 R2
               LDL R4 Result_Add_2.L
               LDH R4 Result_Add_2.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
                 
               SEC                     ; Test SUB
               SUB R3 R1 R2
               LDL R4 Result_Sub_1.L
               LDH R4 Result_Sub_1.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
               
               CLC                     ; Test SBC - Carry 0
               SBC R3 R1 R2
               LDL R4 Result_Sub_1.L
               LDH R4 Result_Sub_1.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
               
               SEC                     ; Test SBC - Carry 1
               SBC R3 R1 R2
               LDL R4 Result_Sub_2.L
               LDH R4 Result_Sub_2.H 
               LD R4 [R4]
               CMP R3 R4
               BZC [R5]
               
               LDL R1 Operand1_1.L     ; Test LD
               LDH R1 Operand1_1.H
               LD R1 [R1]
               LD R2 R1
               LDL R4 Operand1_1.L
               LDH R4 Operand1_1.H
               LD R4 [R4]
               CMP R2 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test LDL
               LDH R1 Operand1_2.H
               LD R1 [R1]
               LDL R2 Operand2_2.L
               LDH R2 Operand2_2.H
               LD R2 [R2]
               LDL R1 R2
               LDL R4 Result_Ldl_1.L
               LDH R4 Result_Ldl_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test LDH
               LDH R1 Operand1_2.H
               LD R1 [R1]
               LDL R2 Operand2_2.L
               LDH R2 Operand2_2.H
               LD R2 [R2]
               LDH R1 R2
               LDL R4 Result_Ldh_1.L
               LDH R4 Result_Ldh_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test LDL imm
               LDH R1 Operand1_2.H
               LD R1 [R1]
               LDL R1 0x12
               LDL R4 Result_Ldl_1.L
               LDH R4 Result_Ldl_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test LDH imm
               LDH R1 Operand1_2.H
               LD R1 [R1]
               LDH R1 0x12
               LDL R4 Result_Ldh_1.L
               LDH R4 Result_Ldh_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDLZ R1 0xAB            ; Test LDLZ imm
               LDL R4 Result_Ldlz_1.L
               LDH R4 Result_Ldlz_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDHZ R1 0xBA            ; Test LDHZ imm
               LDL R4 Result_Ldhz_1.L
               LDH R4 Result_Ldhz_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test SWP
               LDH R1 Operand1_2.H
               LD R1 [R1]
               SWP R1 R1
               LDL R4 Result_Swp_1.L
               LDH R4 Result_Swp_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               
               LDL R1 Operand1_2.L     ; Test INC
               LDH R1 Operand1_2.H
               LD R1 [R1]
               INC R1 R1
               LDL R4 Result_Inc_1.L
               LDH R4 Result_Inc_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]

               LDL R1 Operand1_2.L     ; Test DEC
               LDH R1 Operand1_2.H
               LD R1 [R1]
               DEC R1 R1
               LDL R4 Result_Dec_1.L
               LDH R4 Result_Dec_1.H
               LD R4 [R4]
               CMP R1 R4
               BZC [R5]
               

               ; Test succeeded
               LDL R4 TestOk.L
               LDH R4 TestOk.H
               LDL R5 0x34
               LDH R5 0x12
               STO R5 [R4]
               HLT
       
Error:         ; Test failed
               LDL R4 TestOk.L
               LDH R4 TestOk.H
               LDLZ R5 0
               STO R5 [R4]
               HLT
            
Operand1_1:    .data 100
Operand2_1:    .data 13
Operand1_2:    .data 0xFEDC
Operand2_2:    .data 0x12
Result_Add_1:  .data 100 + 13
Result_Add_2:  .data 100 + 13 + 1
Result_Sub_1:  .data 100 - 13
Result_Sub_2:  .data 100 - 13 - 1
Result_Ldl_1:  .data 0xFE12
Result_Ldh_1:  .data 0x12DC
Result_Ldlz_1: .data 0x00AB
Result_Ldhz_1: .data 0xBA00
Result_Swp_1:  .data 0xDCFE
Result_Inc_1:  .data 0xFEDC + 1
Result_Dec_1:  .data 0xFEDC - 1
</textarea>
</body>
</html>



